buildscript {
    dependencies {
        classpath("org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11")
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

group 'np-docs'
version '1.0-SNAPSHOT'

asciidoctor {
    backends = ['html5', 'pdf']

    sourceDir = file('src')
    outputDir = file('.')

    def files = [
            '*.adoc'
    ]

    sources {
        include files
    }

    attributes \
            'source-highlighter': 'coderay',
            'encoding': 'utf-8',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'idprefix': '',
            'idseparator': '-',
            'pdf-stylesdir': 'theme',
            'pdf-style': 'finki',
            'pdf-fontsdir': 'fonts'

    dependsOn "srcZip"
}

task srcZip

(1..3).each { id ->
    task "srcZip${id}"(type: Zip) {
        from('../examples/src/main/java/mk/ukim/finki/np/av' + id)
        excludes = []
        archiveName = "np_av${id}_src.zip"
        destinationDir = file('./sources')
    }
    srcZip.dependsOn "srcZip${id}"
}